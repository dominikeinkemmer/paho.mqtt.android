apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
group = 'com.github.dominikeinkemmer'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 33

        testApplicationId "org.eclipse.paho.android.service.test"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        preDexLibraries = false
    }

    lintOptions {
        abortOnError false
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'

    libraryVariants.all { variant ->
        def name = variant.name.capitalize()
        task "jar${name}"(type: Jar) {
            from variant.javaCompile.destinationDir
            archiveName = "paho-${variant.name}.jar"
        }
    }
}

repositories {
    maven { url rootProject.ext.mavenUrl }
}

configurations {
   //androidTestCompile.exclude module: 'org.eclipse.paho.client.mqttv3'
}

dependencies {
    implementation "org.eclipse.paho:org.eclipse.paho.client.mqttv3:${rootProject.ext.clientVersion}"
    implementation "com.android.support:support-v4:25.3.1"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}

// Generate Sources Jar
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

// Copy the Paho Java client into the libs directory to bundle it with the AAR
task("copyLibs", type: Copy, dependsOn: clean) {
    println "Copying MQTT Jar into libs directory"
    from configurations.compile
    into 'libs'
    include 'org.eclipse.paho*'
}

// Renames the Release AAR file to a maven convention
task("renameReleaseAAR", type: Copy, dependsOn: assemble) {
    from("$buildDir/outputs/aar")
    into("$buildDir/outputs/aar/")
    include('org.eclipse.paho.android.service-debug.aar')
    rename('org.eclipse.paho.android.service-debug.aar', rootProject.ext.serviceArchivesBaseName + '-' + rootProject.ext.serviceVersion + '.aar')
}

// Generate Plain Jar Version of library (good for legacy users)
task("generateLibraryJar", type: Jar) {
    from("$buildDir/intermediates/classes/release")
}

publishing {
    publications {
        paho(MavenPublication) {
            groupId 'org.eclipse.paho'
            artifact sourcesJar
            artifact generateLibraryJar
            artifact "$buildDir/outputs/aar/" + rootProject.ext.serviceArchivesBaseName + "-" + rootProject.ext.serviceVersion + ".aar"
        }
    }
    repositories {
        if (project.hasProperty('eclipseRepoUsername')) {
            maven {
                name 'eclipse'
                url 'https://repo.eclipse.org/content/repositories/paho-' + (rootProject.ext.serviceVersion.endsWith('-SNAPSHOT') ? 'snapshots/' : 'releases/')
                credentials {
                    username eclipseRepoUsername
                    password eclipseRepoPassword
                }
            }
        } else {
            maven {
                url "$buildDir/repo"
            }
        }
    }
}

artifacts {
    sourcesJar
}

task debug {
    doLast {
        configurations.compile.each { println it}
    }
}

clean.doFirst {
    delete 'libs'
}
